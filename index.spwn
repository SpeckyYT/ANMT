extract obj_props

// https://github.com/SpeckyYT/SPWN-canvas
canvas = import '../canvas/canvas.spwn'
// https://github.com/SpeckyYT/SPWN-speed
speed = import '../speed/speed.spwn'

filename = 'changeyourfilenamehere'

let file = $.readfile('./videos/output/'+filename+'.txt')
let frames = file.split('\n')
let data = frames.shift().split(',')

let width = data[0] as @number
let height = data[1] as @number
let fps = data[2] as @number
let scaling = 10/height

let startdelay = 2
let enddelay = 1

let mySpeed = speed(1)

let screen = ?g

videoCanvas = canvas::new(
    0, 0,
    width, height,
    $.floor(100*scaling)/100,
    screen
)

rgbToInt = (red:@number,green:@number,blue:@number){
    return red*65536 + green*256 + blue 
}

let previousFrame = []
let white = rgbToInt(255,255,255);
for _ in ..(width*height) {
    previousFrame.push(white)
}

for index in ..frames.length {
    $.print(index,' frames done')
    let frame = frames[index]
    let toChange = []
    for pixel in frame.split(':') {
        pixelData = pixel.split(',')

        x = pixelData[0] as @number
        y = pixelData[1] as @number
        r = pixelData[2] as @number
        g = pixelData[3] as @number
        b = pixelData[4] as @number

        key = y*width+x
        color = rgbToInt(r,g,b)
        if previousFrame[key] == color {continue}
        previousFrame[key] = color

        toChange.push([
            videoCanvas.colors[x][height-(y+1)],
            r,g,b
        ])
    }
    for i in ..toChange.length {
        $.add(obj{
            OBJ_ID: 899,
            X:  (index * 1/fps + startdelay) * (30 * mySpeed),
            Y: 30*i,
            DURATION: 0,
            TARGET_COLOR: toChange[i][0],
            TRIGGER_RED: toChange[i][1],
            TRIGGER_GREEN: toChange[i][2],
            TRIGGER_BLUE: toChange[i][3],
        })

    }
}

$.add(obj{
    OBJ_ID: 1,
    X:  (frames.length * 1/fps + startdelay + enddelay) * (30 * mySpeed),
    Y: -30,
})

hide_player()
screen.lock_to_player(true,false)
toggle_bg_effect(false)
-> screen.move(-45,0,1)

for color in [BG,GROUND,LINE] {
    color.set(0,0,0)
}
